function(download_and_extract NAME URL OUT_DIR)
  set(DOWNLOAD_DIR "${CMAKE_BINARY_DIR}/_deps")
  set(EXTRACT_DIR "${DOWNLOAD_DIR}/${NAME}")
  set(ARCHIVE_FILE "${DOWNLOAD_DIR}/${NAME}.archive")
  set(URL_FILE "${EXTRACT_DIR}/.url")

  if(EXISTS "${URL_FILE}")
    file(READ "${URL_FILE}" CACHED_URL)
    if("${CACHED_URL}" STREQUAL "${URL}")
      file(GLOB CONTENT_DIRS LIST_DIRECTORIES true "${EXTRACT_DIR}/*")
      list(FILTER CONTENT_DIRS EXCLUDE REGEX ".*\\.url$")
      list(GET CONTENT_DIRS 0 CONTENT_DIR)
      set(${OUT_DIR} "${CONTENT_DIR}" PARENT_SCOPE)
      return()
    else()
      message(STATUS "URL changed for ${NAME}, cleaning up...")
      file(REMOVE_RECURSE "${EXTRACT_DIR}")
      file(REMOVE "${ARCHIVE_FILE}")
    endif()
  endif()

  if(NOT EXISTS "${ARCHIVE_FILE}")
    message(STATUS "Downloading ${NAME}...")
    file(DOWNLOAD "${URL}" "${ARCHIVE_FILE}" SHOW_PROGRESS STATUS DOWNLOAD_STATUS)
    list(GET DOWNLOAD_STATUS 0 STATUS_CODE)
    if(NOT STATUS_CODE EQUAL 0)
      file(REMOVE "${ARCHIVE_FILE}")
      message(FATAL_ERROR "Failed to download ${URL}")
    endif()
  endif()

  if(NOT EXISTS "${EXTRACT_DIR}" OR NOT EXISTS "${URL_FILE}")
    message(STATUS "Extracting ${NAME}...")
    file(ARCHIVE_EXTRACT INPUT "${ARCHIVE_FILE}" DESTINATION "${EXTRACT_DIR}")
    file(WRITE "${URL_FILE}" "${URL}")
  endif()

  file(GLOB CONTENT_DIRS LIST_DIRECTORIES true "${EXTRACT_DIR}/*")
  list(FILTER CONTENT_DIRS EXCLUDE REGEX ".*\\.url$")
  list(GET CONTENT_DIRS 0 CONTENT_DIR)

  set(${OUT_DIR} "${CONTENT_DIR}" PARENT_SCOPE)
endfunction()
